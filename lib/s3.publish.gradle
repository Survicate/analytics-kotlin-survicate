/**
 * Steps to publish:
 * 1. Bump lib_version inside the build.gradle of the lib module.
 * 2. Publish the SDK locally to test the artifact build process: ./gradlew :lib:cleanBuildDocPublishLocal
 * 3. Publish to Survicate Maven by running the "Publish SDK" workflow.
 */
tasks.register('androidJavadocsJar', Jar) {
    dependsOn dokkaJavadoc
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

publishing {
    repositories {
        maven {
            name = 'survicateMaven'
            url "s3://repo.survicate.com/"

            authentication {
                // role based authentication
                awsIm(AwsImAuthentication)
            }
        }
    }

    publications {
        release(MavenPublication) {
            groupId 'com.survicate'
            artifactId 'survicate-segment-analytics-kotlin'
            version lib_version

            artifact androidJavadocsJar

            pom {
                name = "Analytics-Kotlin Survicate"
                url = "https://github.com/Survicate/analytics-kotlin-survicate"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
            }

            afterEvaluate {
                from components.release
            }
        }
    }
}

// Publish to Survicate S3 maven
tasks.register('cleanBuildDocPublish', GradleBuild) {
    tasks = ['clean', 'assembleRelease', 'publishReleasePublicationToSurvicateMavenRepository']
}

// Publish locally into ~/.m2/repository
task cleanBuildDocPublishLocal(type: GradleBuild) {
    tasks = ['clean', 'assembleRelease', 'publishReleasePublicationToMavenLocal']
}
